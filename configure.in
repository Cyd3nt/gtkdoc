dnl Process this file with autoconf to produce a configure script.

AC_INIT(gtk-doc.dsl.in)

AM_INIT_AUTOMAKE(gtk-doc, 0.5)

AC_ARG_WITH(dsssl-dir, [  --with-dsssl-dir=PATH path to DocBook style sheets ])

AC_ISC_POSIX
AC_PROG_CC

if test "x$with_dsssl_dir" = "x" ; then
  dirs="/usr/lib/sgml/stylesheets/nwalsh-modular /usr/lib/sgml/stylesheet/dsssl/docbook/nwalsh /usr/lib/dsssl/stylesheets/docbook /usr/share/sgml/docbkdsl"
else
  dirs=$with_dsssl_dir
fi
 
AC_PATH_PROG(PERL, perl)
if test -z "$PERL"; then
   AC_MSG_ERROR([perl not found])
fi

AC_MSG_CHECKING([for DocBook stylesheets])

DSSSL_DIR=""
for i in $dirs ; do
  if test -f $i/html/docbook.dsl ; then
    DSSSL_DIR=$i
  fi
done

if test "x$DSSSL_DIR" = x ; then
  AC_MSG_ERROR([
*** Cannot find DocBook stylesheets in "$dirs". 
*** Perhaps you need to specify the --with-dsssl-dir=PATH option?])
fi

AC_SUBST(DSSSL_DIR)

AC_MSG_RESULT(found)


dnl Set PACKAGE_DATA_DIR so we can find the script containing common routines.
if test "x${datadir}" = 'x${prefix}/share'; then
  if test "x${prefix}" = "xNONE"; then
    PACKAGE_DATA_DIR="${ac_default_prefix}/share/${PACKAGE}"
  else
    PACKAGE_DATA_DIR="${prefix}/share/${PACKAGE}"
  fi
else
  PACKAGE_DATA_DIR="${datadir}/${PACKAGE}"
fi

AC_SUBST(PACKAGE_DATA_DIR)


dnl Only use -Wall if we have gcc
if test "x$GCC" = "xyes"; then
  if test -z "`echo "$CFLAGS" | grep "\-Wall" 2> /dev/null`" ; then
    CFLAGS="$CFLAGS -Wall"
  fi
fi

AC_OUTPUT([Makefile 
gtk-doc.dsl 
gtk-doc.spec
gtkdoc-common.pl
gtkdoc-mkman 
gtkdoc-mkhtml
gtkdoc-fixxref
gtkdoc-mkdb
gtkdoc-mktmpl
gtkdoc-scan
gtkdoc-scanobj
gtkdoc-scangobj
],
[chmod +x gtkdoc-mkman gtkdoc-mkhtml gtkdoc-fixxref gtkdoc-mkdb gtkdoc-mktmpl gtkdoc-scan gtkdoc-scanobj])
