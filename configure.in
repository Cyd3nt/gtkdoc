dnl Process this file with autoconf to produce a configure script.

AC_INIT(gtk-doc.dsl.in)

AM_INIT_AUTOMAKE(gtk-doc, 0.10)

AC_ISC_POSIX
AC_PROG_CC

dnl
dnl Check for Perl.
dnl
AC_PATH_PROG(PERL, perl)
if test -z "$PERL"; then
  AC_MSG_ERROR([perl not found])
fi

AC_MSG_CHECKING([if Perl version >= 5.6.0])
if "$PERL" -e "require v5.6.0"; then
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
  AC_MSG_ERROR([perl >= 5.6.0 is required for gtk-doc])
fi

dnl
dnl Check for jade or openjade.
dnl
AC_PATH_PROG(JADE, openjade)
if test -z "$JADE"; then
  AC_PATH_PROG(JADE, jade)
  if test -z "$JADE"; then
    AC_MSG_ERROR([openjade/jade not found])
  fi
fi

dnl
dnl Check for xsltproc
dnl
AC_PATH_PROG(XSLTPROC, xsltproc)
if test -z "$XSLTPROC"; then
  AC_MSG_ERROR([xsltproc not found])
fi

dnl
dnl Set PACKAGE_DATA_DIR so we can find the script containing common routines.
dnl
if test "x${datadir}" = 'x${prefix}/share'; then
  if test "x${prefix}" = "xNONE"; then
    PACKAGE_DATA_DIR="${ac_default_prefix}/share/${PACKAGE}"
  else
    PACKAGE_DATA_DIR="${prefix}/share/${PACKAGE}"
  fi
else
  PACKAGE_DATA_DIR="${datadir}/${PACKAGE}"
fi

AC_SUBST(PACKAGE_DATA_DIR)


dnl Only use -Wall if we have gcc
if test "x$GCC" = "xyes"; then
  if test -z "`echo "$CFLAGS" | grep "\-Wall" 2> /dev/null`" ; then
    CFLAGS="$CFLAGS -Wall"
  fi
fi


AC_OUTPUT([Makefile 
gtk-doc.dsl 
gtk-doc.spec
gtkdoc-common.pl
gtkdoc-mkman 
gtkdoc-mkhtml
gtkdoc-fixxref
gtkdoc-mkdb
gtkdoc-mktmpl
gtkdoc-scan
gtkdoc-scanobj
gtkdoc-scangobj
],
[chmod +x gtkdoc-mkman gtkdoc-mkhtml gtkdoc-fixxref gtkdoc-mkdb gtkdoc-mktmpl gtkdoc-scan gtkdoc-scanobj])
